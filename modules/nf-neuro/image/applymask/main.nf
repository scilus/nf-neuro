process IMAGE_APPLYMASK {
    tag "$meta.id"
    label 'process_single'

    container "mrtrix3/mrtrix3:3.0.5"

    input:
        tuple val(meta), path(image), path(mask)

    output:
        tuple val(meta), path("*masked.nii.gz")     , emit: image
        path "versions.yml"                         , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def prefix = task.ext.prefix ?: "${meta.id}"
    def suffix = task.ext.first_suffix ? task.ext.first_suffix + "_masked"  : "masked"
    def nthreads_mrtrix = task.ext.single_thread ? "-nthreads 0" : "-nthreads ${task.cpus}"
    def data_type = task.ext.data_type ? "-datatype ${task.ext.data_type}" : "-datatype float32"

    """
    export OMP_NUM_THREADS=${task.ext.single_thread ? 1 : task.cpus}

    mrcalc $image $mask -mult ${prefix}_${suffix}.nii.gz -force -quiet ${nthreads_mrtrix} ${data_type}

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        mrtrix: \$(mrcalc -version 2>&1 | sed -n 's/== mrcalc \\([0-9.]\\+\\).*/\\1/p')
    END_VERSIONS
    """

    stub:
    def prefix = task.ext.prefix ?: "${meta.id}"
    def suffix = task.ext.first_suffix ? task.ext.first_suffix + "_masked"  : "masked"
    """
    touch ${prefix}_${suffix}.nii.gz

    mrcalc -h

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        mrtrix: \$(mrcalc -version 2>&1 | sed -n 's/== mrcalc \\([0-9.]\\+\\).*/\\1/p')
    END_VERSIONS
    """
}
